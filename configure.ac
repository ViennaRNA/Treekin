dnl Autoconf initialization.
dnl Bug reports are sent to mtw@tbi.univie.ac.at

AC_INIT([Treekin],
        m4_esyscmd_s([cat VERSION.txt]),
        [mtw@tbi.univie.ac.at], [Treekin], [http://www.tbi.univie.ac.at/RNA/Treekin])

AC_CONFIG_AUX_DIR(config)

AC_CONFIG_MACRO_DIR([m4])

AC_CONFIG_SRCDIR([src/calc.cpp])
AC_CONFIG_HEADERS(config.h)

AM_SILENT_RULES([yes])

AM_INIT_AUTOMAKE(-Wall -Werror)

AC_CANONICAL_HOST

TestDataDirectory="\$(srcdir)"
dnl SHAREDLIBEXT="so"

case $host_os in
  *linux*)
    HOST_TYPE="linux"
    SHAREDLIBEXT="so"
    ;;

  *solaris*)
    HOST_TYPE="solaris"
    SHAREDLIBEXT="so"
    ;;

  *cygwin*)
    HOST_TYPE="cygwin"
    SHAREDLIBEXT="so"
    ;;

  *darwin*)
    HOST_TYPE="darwin"
    SHAREDLIBEXT="dylib"
    TestDataDirectory="."
    ;;
esac
AC_SUBST(TestDataDirectory)
AC_SUBST(HOST_TYPE)
AC_SUBST(SHAREDLIBEXT)
AC_SUBST(CFLAGS)


AC_DEFUN([AC_TREEKIN_APPEND_VAR_COMMA],[
  if test "x$$1" != "x" ; then
    $1="${$1}, "
  fi
  $1="${$1}$2"
])



dnl ---------------------------------------------------------------------------
dnl Checks for various programs and packages.
dnl ---------------------------------------------------------------------------

AC_PROG_CXX
AC_PROG_CC
AC_PROG_RANLIB
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AC_PATH_PROG([AUTOCONF],[autoconf],[autoconf])
AC_PATH_PROG([ACLOCAL],[aclocal],[aclocal])
AC_PATH_PROG([MAKE],[make],[make])
AC_PATH_PROG([AR],[ar],[ar])

AX_CXX_COMPILE_STDCXX([11])

dnl ---------------------------------------------------------------------------
dnl Checks for gengetopt
dnl ---------------------------------------------------------------------------

AC_ARG_VAR([GENGETOPT], [the 'gengetopt' program to generate command line argument parsers for executable programs])
AC_PATH_PROG([GENGETOPT], [gengetopt], [no])
AC_SUBST([GENGETOPT])
AM_CONDITIONAL(TREEKIN_AM_SWITCH_HAS_GENGETOPT, test "x$GENGETOPT" != "xno")


dnl ---------------------------------------------------------------------------
dnl Checks for libraries.
dnl ---------------------------------------------------------------------------

AX_LAPACK([
      AC_CHECK_HEADERS([openblas/lapacke.h lapacke/lapacke.h lapacke.h],[treekin_lapacke_header=yes;break;],)
      AS_IF([test "x$treekin_lapacke_header" = "xyes"],[],[
        AC_MSG_ERROR([could not find lapacke.h])
      ])
      ],[AC_MSG_ERROR([could not find required version of Lapack library])])

dnl AC_CHECK_LIB(m, isnan)
dnl AC_CHECK_LIB(arpack,dseupd_)

dnl ---------------------------------------------------------------------------
dnl Checks for header files.
dnl ---------------------------------------------------------------------------

AC_HEADER_STDC
AC_CHECK_HEADERS(errno.h)

dnl Check for glib-2.0 using pkgconfig
dnl PKG_CHECK_MODULES([GLIB], [glib-2.0], [have_glib=true], [have_glib=false])
dnl if test "x${have_glib}" = "xfalse" ; then
dnl         AC_MSG_ERROR([No Glib package information found])
dnl fi
dnl AC_SUBST(GLIB_CFLAGS)
dnl AC_SUBST(GLIB_LIBS)

AC_CHECK_HEADERS(math.h)

dnl ---------------------------------------------------------------------------
dnl Check for mlapack
dnl ---------------------------------------------------------------------------

AC_TREEKIN_MPACK

dnl ---------------------------------------------------------------------------
dnl Checks for typedefs, structures, and compiler characteristics.
dnl ---------------------------------------------------------------------------

AC_C_BIGENDIAN
AC_C_CONST
AC_TYPE_SIZE_T
AC_EXEEXT
AC_OBJEXT

dnl ---------------------------------------------------------------------------
dnl Miscellaneous
dnl ---------------------------------------------------------------------------

AC_ARG_VAR([HELP2MAN], [the 'help2man' script to generate man pages from command line options of our executable programs])
AC_PATH_PROG([HELP2MAN], [help2man], [no])
AC_SUBST([HELP2MAN])
AM_CONDITIONAL(TREEKIN_AM_SWITCH_HAS_HELP2MAN, test "x$HELP2MAN" != "xno")

# Root directory
# Translate path for native Windows compilers for use with 'make check'

ROOT_DIR=`pwd`
case $host in
*-*-cygwin* | *-*-mingw*)
  if (cygpath --mixed $ROOT_DIR) >/dev/null 2>/dev/null; then
    ROOT_DIR=`cygpath --mixed $ROOT_DIR`
  fi
  # Extra files generated by some Windows compilers
  EXTRA_CLEAN="*.stackdump *.exp *.lib"
  ;;
esac

dnl ---------------------------------------------------------------------------
dnl Output
dnl ---------------------------------------------------------------------------

AC_CONFIG_FILES([Makefile src/Makefile])
AC_CONFIG_FILES([packaging/treekin.spec packaging/PKGBUILD])
AC_CONFIG_FILES([man/Makefile man/cmdlopt.sh],[chmod +x man/cmdlopt.sh])

AC_OUTPUT

dnl ---------------------------------------------------------------------------
dnl Inform users of settings
dnl ---------------------------------------------------------------------------

# get directory paths

eval _bindir=$(eval printf "%s" $bindir)
eval _libdir=$(eval printf "%s" $libdir)
eval _includedir=$(eval printf "%s" $includedir)
eval _datadir=$(eval printf "%s" $datadir)
eval _mandir=$(eval printf "%s" $mandir)

if test "x$mpack_wanted_but_failed" != "x" || test "x$enable_mpack" != "xyes" ; then
  drtrafo_note="Note, that the most recent version of the DrTransformer program requires Treekin to be linked against the MPACK library!"
else
  if test "x$mpack_data_types" != "x"; then
    mpack_types="( Supported Data Types: $mpack_data_types )"
  fi
fi

AC_MSG_NOTICE(
[

======================================
    Treekin ${PACKAGE_VERSION}
======================================

Configured successful with the following options:

Extra Libraries
---------------
  * MPack             : ${enable_mpack:-no} $mpack_wanted_but_failed $mpack_types

Install Directories
-------------------
  * Executables       : $_bindir
  * Libraries         : $_libdir
  * Header files      : $_includedir
  * Extra Data        : $_datadir
  * Man pages         : $_mandir

$drtrafo_note

You can run make now.
])
